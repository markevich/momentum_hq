defmodule MomentumHq.MissionControl.RenderTasksForDay do
  alias MomentumHq.MissionControl
  alias MomentumHq.Telegram

  use MomentumHq.Constants

  def render_and_send_new(user, date) do
    {user_tasks, user_tasks_by_momentums} = user_tasks_grouped_by_momentums(user.id, date)

    render_today_message(
      user.telegram_id,
      length(user_tasks),
      length(Map.keys(user_tasks_by_momentums))
    )
    |> Telegram.send_user_message_async()

    user_tasks_by_momentums
    |> Enum.each(fn {momentum_name, tasks} ->
      render_momentum_with_tasks(user.telegram_id, momentum_name, tasks)
      |> Telegram.send_user_message_async()
    end)
  end

  def rerender_by_task_tg_callback(task, telegram_message_id) do
    user = task.user
    date = task.target_date

    momentum_name = task.momentum.name
    tasks = MissionControl.get_user_tasks_for_a_day(user.id, date, task.momentum_id)

    render_momentum_with_tasks(user.telegram_id, momentum_name, tasks)
    |> Map.put(:message_id, telegram_message_id)
    |> Telegram.update_user_message()
  end

  defp user_tasks_grouped_by_momentums(user_id, date) do
    user_tasks = MissionControl.get_user_tasks_for_a_day(user_id, date)

    user_tasks_by_momentums =
      user_tasks
      |> Enum.group_by(fn task ->
        task.momentum.name
      end)

    {user_tasks, user_tasks_by_momentums}
  end

  defp render_today_message(chat_id, tasks_count, momentums_count) do
    %{
      chat_id: chat_id,
      text: """
      Ğ”Ğ¾Ğ±Ñ€Ğ¾Ğµ ÑƒÑ‚Ñ€Ğ¾\\! Ğ¡ĞµĞ³Ğ¾Ğ´Ğ½Ñ Ñƒ Ğ½Ğ°Ñ Ğ² Ñ€Ğ°ÑĞ¿Ğ¸ÑĞ°Ğ½Ğ¸Ğ¸ #{tasks_count} Ğ·Ğ°Ğ´Ğ°Ñ‡ Ğ² #{momentums_count} Ğ¼Ğ¾Ğ¼ĞµĞ½Ñ‚ÑƒĞ¼Ğ°Ñ…\\.

      Ğ›ĞµĞ³ĞµĞ½Ğ´Ğ°:
      ```
      âœ… - Ğ’Ñ‹Ğ¿Ğ¾Ğ»Ğ½ĞµĞ½Ğ°
      ğŸŸ¡ - ĞĞ¶Ğ¸Ğ´Ğ°ĞµÑ‚ Ğ²Ñ‹Ğ¿Ğ¾Ğ»Ğ½ĞµĞ½Ğ¸Ñ
      âŒ - ĞĞµ Ğ²Ñ‹Ğ¿Ğ¾Ğ»Ğ½ĞµĞ½Ğ°

      - ĞĞ°Ğ¶Ğ¼Ğ¸ Ğ½Ğ° ĞºĞ½Ğ¾Ğ¿ĞºÑƒ Ñ‡Ñ‚Ğ¾Ğ±Ñ‹ Ğ¿Ğ¾Ğ¼ĞµĞ½ÑÑ‚ÑŒ ÑÑ‚Ğ°Ñ‚ÑƒÑ Ğ·Ğ°Ğ´Ğ°Ñ‡Ğ¸.
      - ğŸŸ¡ Ğ¿Ñ€ĞµĞ²Ñ€Ğ°Ñ‚Ğ¸Ñ‚ÑÑ Ğ² âŒ Ğ·Ğ°Ğ²Ñ‚Ñ€Ğ° ÑƒÑ‚Ñ€Ğ¾Ğ¼. 
      ```

      """,
      disable_notification: true
    }
  end

  def render_momentum_with_tasks(chat_id, momentum_name, tasks) do
    momentum_message = "ĞœĞ¾Ğ¼ĞµĞ½Ñ‚ÑƒĞ¼: `#{Telegram.escape_telegram_markdown(momentum_name)}`"

    tasks_keyboard =
      tasks
      |> Enum.map(fn task ->
        status =
          case task.status do
            :pending -> "ğŸŸ¡"
            :completed -> "âœ…"
            :failed -> "âŒ"
          end

        [
          %{
            text: "#{status} #{task.icon} #{task.name}",
            callback_data:
              Jason.encode!(%{
                p: @cycle_task_status,
                id: task.id
              })
          }
        ]
      end)

    %{
      chat_id: chat_id,
      text: momentum_message,
      reply_markup: %{
        inline_keyboard: tasks_keyboard
      },
      disable_notification: true
    }
  end
end
